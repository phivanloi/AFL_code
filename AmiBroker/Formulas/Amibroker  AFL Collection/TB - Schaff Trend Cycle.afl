//|------------------------------------------------------------------
//|Tradingbasis.com - Schaff Trend Cycle indicator								
//|Version:1.1   31-March-2006
//|Copyright © 2005, Tradingbasis.com
//|http://www.tradingbasis.com
//|e-mail: support@tradingbasis.com
//|------------------------------------------------------------------
SetChartOptions(0,chartShowDates|chartWrapTitle,Chartgridmiddle==0);
#pragma nocache
#include  <\TBInc\Tradingbasis_f0.afl>
_SECTION_BEGIN("Build-In Average"); 
AddAvgTypeParam(3);
_SECTION_END(); 
_SECTION_BEGIN("First Time Frame"); 
TC =Param("TC Period",10,1,100,1);
MA1 =Param("MA1 Period",23,5,50,1);
MA2=Param("MA2 Period",50,20,200,1);
LowerLevel1 = Param("Lower level",25,5,35,1);
UpperLevel1 = Param("Upper level",75,65,95,1);
STCstyle = ParamStyle("Style",styleLine|styleThick,maskDefault);
Color = ParamColor("Color",colorLime);
STC1 = STC(TC,MA1,MA2,Avgtype);
Plot(STC1,"STC",Color,STCstyle);
_SECTION_END(); 
_SECTION_BEGIN("Second Time Frame"); 
TF = Param("TF-Multiplier",4,0,24,1);
TC =Param("TC Period",10,1,100,1);
MA1 =Param("MA1 Period",23,5,50,1);
MA2=Param("MA2 Period",50,20,200,1);
LowerLevel2 = Param("Lower level",25,5,35,1);
UpperLevel2 = Param("Upper level",75,65,95,1);
STCstyle = ParamStyle("Style",styleLine|styleThick,maskDefault);
Color2 = ParamColor("Color",colorYellow);
TimeFrameSet(Interval()*TF);
STC_TF2 = STC(TC,MA1,MA2,Avgtype);
TimeFrameRestore();
STC_TF2 = TimeFrameExpand(STC_TF2,TF*Interval(),expandFirst);
Plot(STC_TF2,"STC TF2",Color2,STCstyle);
_SECTION_END(); 

//First time frame
WB = Cross(STC1,LowerLevel1);
SB = Cross(STC1,UpperLevel1);
WS = Cross(UpperLevel1,STC1);
SS = Cross(LowerLevel1,STC1);
WB_TF2 = Cross(STC_TF2,LowerLevel2);
SB_TF2 = Cross(STC_TF2,UpperLevel2);
WS_TF2 = Cross(UpperLevel2,STC_TF2);
SS_TF2 = Cross(LowerLevel2,STC_TF2);

STCValley = Ref(STC1,-1)==LLV(STC1,3)AND STC1<50;
STCTop = Ref(STC1,-1)==HHV(STC1,3) AND STC1>50;

Buy = STC_TF2 > UpperLevel2 AND STC_TF2>=Ref(STC_TF2,-1) AND STCValley;
Sell = STC_TF2 < LowerLevel2 AND STC_TF2<=Ref(STC_TF2,-1) AND STCTop;

Filter = NOT GroupID()==253;
Filter = Filter AND (WB|WS|SB|SS|WB_TF2|WS_TF2|SB_TF2|SS_TF2);


TC = IIf(WB|SB|WB_TF2|SB_TF2,colorGreen,IIf(WS|SS|WS_TF2|SS_TF2,colorRed,Null));
BC = IIf(WB|SB|WB_TF2|SB_TF2,colorGreen,IIf(WS|SS|WS_TF2|SS_TF2,colorRed,Null));
BC = colorDefault;
Adddefaultcolumns(2,1);

AddColumn( IIf(WB, 66, IIf(WS,83,Null) ), "Weak Signal",formatChar,TC,BC,80);
AddColumn( IIf(SB, 66, IIf(SS,83,Null) ), "Strong Signal", formatChar,TC,BC,80);
AddColumn( IIf(WB_TF2, 66, IIf(WS_TF2,83,Null) ), "TF2 Weak Signal",formatChar,TC,BC,100);
AddColumn( IIf(SB_TF2, 66, IIf(SS_TF2,83,Null) ), "TF2 Strong Signal", formatChar,TC,BC,100);
AddTextColumn(FullName(),"Full name");
PlotGrid(LowerLevel1,colorDefault);
PlotGrid(UpperLevel1,colorDefault);
PlotGrid(LowerLevel2,colorDefault);
PlotGrid(UpperLevel2,colorDefault);

Title = 
"Tradingbasis.com - Schaff Trend Cycle" + " - " +  Name() + " - " + 
EncodeColor(colorRed)+ Interval(2) + EncodeColor() + "  - " + Date() +" - " + EncodeColor(Color)+
"STC = "+WriteVal(STC1,1.2)+ ", " +EncodeColor(color2)+"STC 2nd time frame = "+WriteVal(STC_TF2,1.2);