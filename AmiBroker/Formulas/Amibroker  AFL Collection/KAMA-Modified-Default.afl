// KAMA System by Karthikmarar
// Two adjustable parameter "Buy sensitivity" and "Buy Finetune" provided to adjust entry points.
// Two adjustable parameter "Sell sensitivity" and "Sell Finetune" provided to adjust Exit points.
MyWL = 14;
_SECTION_BEGIN("Filters");		// Volume & TurnOver Parameters
	MyCL  	= Param( "Price Filter (Rs)", 50, 10, 100, 10 );
	MyVK 	= Param( "Volume Filter (1000s)", 50, 10, 100, 10 );
	MyTL 	= Param( "TurnOver Filter (Lakhs)", 300, 100, 1000, 100 );

	TLM 	= EMA(Close*Volume/100000,100) ;
	include= Close > MyCL AND ( (Volume/1000)> MyVK ) AND ( (Close*Volume/100000)> MyTL ) AND (TLM > 0.333 * MyTL) ;
_SECTION_END();

_SECTION_BEGIN("KAMA System");
/* 	Version 1.1
	Modified on : 28-11-2006
	Original Code : http://www.traderji.com/67020-post435.html
	Reference material : http://trader.online.pl/ELZ/t-s-Adaptive_Moving_Average_System.html
*/

SetBarsRequired(10000, 10000);
SetChartOptions(0, chartShowArrows|chartShowDates);
_N(Title = StrFormat("[KAMA] {{NAME}} - {{INTERVAL}} {{DATE}} Open %g, Hi %g, Lo %g, Close %g (%.1f%%) {{VALUES}}", O, H, L, C, SelectedValue( ROC(C, 1 ) ) ));

// Buy Parameters
bPeriods	= Param("BUY Sensitivity",8,2,20,1);
bF       	= Param("BUY Finetune",2,0.1,20,0.1);
// Sell Parameters
sPeriods  = Param("SELL Sensitivity",3,2,20,1);
sF        = Param("SELL Finetune",1,0.1,20,0.1);
start = ParamDate("sDt","06-12-1990");
end = ParamDate("eDt","12-27-2006");
// common
fast   = 2/(2+1);      // 0.666        // Fast end factor
slow   = 2/(30+1);     // 0.0645       // Slow end factor

//BUY part
bDir   = abs( Close - Ref(Close, -bPeriods) );         	// Signal:=abs(Pr-Ref(Pr,-Periods));
bVol   = Sum( abs(Close - Ref(Close, -1)) , bPeriods);    // Sum of Noise for the given period
bER    = bDir/bVol;                                    	// EffRatio = If(Noise>0.,Signal/Noise,0.);

                                      	  // FERatio = FEndF*EffRatio+SEndF*(1-EffRatio);
bSc		= ( bER*(fast - slow) + slow )^2;   // SmoothF = Power(FERatio,2))
bX     = AMA( Close, bSc );					  // AMA value

/* the Filter itself is based on the volatility
	Filter = k * (change in Kama value)std dev 
*/
bFilter	= bF * StDev( bX - Ref(bX, -1), 20);
j			= bX - Ref(bX, -3);	

//SELL part
sDir	= abs(Close-Ref(Close, -sPeriods));
sVol	= Sum(abs( Close - Ref(Close, -1) ), sPeriods);
sER    = sDir/sVol;
sSc    = ( sER*(fast - slow) + slow)^2;
sX     = AMA( Close, sSc );
sFilter= sF *StDev( sX - Ref(sX, -1), 20);
k      = Ref(sX, -3) - sX;

/*     BUY when the AMA turns up
               and
       SELL when it turns down			*/
Buy     = Cross(j, bFilter) AND INCLUDE ;
Sell    = Cross(k, sFilter) ;

Buy     = ExRem(Buy, Sell);
Sell    = ExRem(Sell, Buy);

/* Copied from AFL Example */
AASettings = Status("action");
if (AASettings == 1) {
	// [Indicator]
	GraphXSpace = 5;
	mycolor = IIf(Close > bX, colorLime, colorRed);
	shape   = Buy * shapeUpArrow + Sell * shapeDownArrow ;

	Plot( bX, "kamaBuy", colorBlue, styleLine, 1);
	Plot( sX, "kamaSell", colorBrown, styleLine, 1);
	Plot( Close, "Close", mycolor, styleNoTitle | styleBar | styleThick   );
	PlotShapes( shape, IIf( Buy, colorGreen, colorRed ),0, IIf( Buy, Low, High ) );

	dist = 1.5*ATR(20);
	for( i = 0; i < BarCount; i++ ) {
       if( Buy[i] ) PlotText( "Buy\n@" + Close[i], i, Low[i] - dist[i], colorGreen );
       if( Sell[i] ) PlotText( "sell\n@" + Close[i], i, Low[i] + dist[i], colorRed );
	}

} else if (AASettings == 4) {
	// [Exploration]
	Filter = Buy OR Sell;
	AddColumn(IIf(Buy,66,83),"Buy/Sell",formatChar,textcolor=IIf( Buy, colorGreen, IIf(Sell, colorRed, colorBlack)));
	AddColumn(Close, "Close", 1.3);
	AddColumn(High, "High", 1.3);
	AddColumn(Volume, "Volume", 1);
	AddColumn(ADX(14), "ADX", 1.4);
	AddColumn(RSI(14), "RSI", 1.4);
	AddColumn(MFI(14), "MFI", 1.4);

} else if (AASettings == 5) {
	// [Backtest] 
	SetFormulaName("KAMA System");
	SetTradeDelays(1, 1, 1, 1);
	PositionScore	=100/Close;
	PositionSize	= - 20;		// 20% of Portfolio
}

_SECTION_END();


// Add to WatchList
CategoryRemoveSymbol( "", categoryWatchlist, MyWL );
if( LastValue( Buy ) /*OR LastValue( Sell )*/ )
{
CategoryAddSymbol( "", categoryWatchlist, MyWL );
}