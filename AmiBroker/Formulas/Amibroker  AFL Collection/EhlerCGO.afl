//	EhlerCGO.afl
//
// John Ehlers, Cybernetic Analysis for Stocks and Futures.
//	Wiley, 2004.
// Chapter 5, p. 47. Code on p. 49.

SetBarsRequired( 200, 0 );

function CGOscillator( array, length )
{
    CGOValue = array;

    for ( i = length; i < BarCount; i++ )
    {
        num = 0;
        denom = 0;

        for ( j = 0; j < length; j++ )
        {
            num = num + ( 1 + j ) * array[i - j];
            denom = denom + array[i - j];
        }

        if ( denom != 0 )
            CGOValue[i] = -num / denom + ( length + 1 ) / 2;
    }

    return CGOValue;
}

med = ( H + L ) / 2;
LookBack = Param( "LookBack", 10, 1, 250, 1 );

dd = CGOscillator( med, LookBack );

Plot( C, "C", colorBlack, styleCandle );
Plot( dd, "CG Oscillator", colorRed, styleLine | styleOwnScale );
