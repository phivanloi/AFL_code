//|------------------------------------------------------------------
//|Tradingbasis.com - MACD - modified
//|Version:1.2   26-May-2006
//|Copyright © 2005, Tradingbasis.com
//|http://www.tradingbasis.com
//|e-mail: support@tradingbasis.com
//|------------------------------------------------------------------
SetChartOptions(0,chartShowDates|chartWrapTitle,chartGridMiddle=0);
#pragma nocache
#include  <\TBInc\Tradingbasis_f0.afl> 

AddAlertParam(0,0,0);
_SECTION_BEGIN("Alerts");
BuySig = ParamList("Buy Signal", List1 = "MACD crosses Signal,MACD crosses zero Level",0); 
SellSig = ParamList("Sell Signal",List2 = "Signal crosses MACD,MACD crosses zero Level",0);
for( i=0; i<10; i++) {if( StrExtract(List1, i) == BuySig ) BuySignal = i+1; } 
for( i=0; i<10; i++) {if( StrExtract(List2, i) == SellSig ) SellSignal = i+1; } 
_SECTION_END();

_SECTION_BEGIN("Main Parameters");
AddAvgTypeParam(1);
r1 = Param( "Fast Average", 6, 2, 100, 1 );
r2 = Param( "Slow Average", 13, 2, 100, 1 );
r3 = Param( "Signal Average", 5, 2, 100, 1 );

MACD_mod = Average(C,r1,AvgType) - Average(C,r2,AvgType);
MACDsignal_mod = Average(MACD_mod,r3,AvgType);
MACDHistogram_mod = MACD_mod-MACDsignal_mod;

_SECTION_BEGIN("Styles");
MACDstyle = ParamStyle("MACD",styleThick);
Signalstyle = ParamStyle("Signal",styleThick);
Histostyle = ParamStyle("Histogram",styleHistogram,maskAll);
_SECTION_END();
_SECTION_BEGIN("Colors");
MACDcolor = ParamColor("MACD", colorRed );
Signalcolor = ParamColor("Signal", colorLightBlue);
Histocolor = ParamColor("Histogram", colorLightGrey);
_SECTION_END();
Plot(MACD_mod, StrFormat(_SECTION_NAME()+"(%g,%g)", r1, r2),MACDcolor,MACDstyle);
Plot(MACDsignal_mod, "Signal" + _PARAM_VALUES(),Signalcolor,Signalstyle);
Plot(MACDHistogram_mod, "MACD Histogram",Histocolor,Histostyle);

Buy = IIf(BuySignal==1,Cross(MACD_mod,MACDsignal_mod),Cross(MACD_mod,0));
Sell = IIf(SellSignal==1,Cross(MACDsignal_mod,MACD_mod),Cross(0,MACD_mod));
Filter = NOT GroupID()==253;
Filter = Filter AND (Buy OR Sell);
Adddefaultcolumns(2,1);
AddTextColumn(FullName(),"Full name");

Alerttext = 
DefaultAlertText+","
+"\nMACD =   "+ WriteVal(MACD_mod)+", "
+"\nSignal =   "+WriteVal(MACDSignal_mod);

AddAlert(Buy,Audio,Email,BuySig + Alerttext,1,BarComplete);
AddAlert(Sell,Audio,Email,SellSig + Alerttext,2,BarComplete);

Title = "Tradingbasis.com - MACD Modified "+AvgText + " - " +  Name() + " - " + EncodeColor(colorRed)+ Interval(2) + EncodeColor() + " " + Date() + ", "+EncodeColor(colorRed) +"MACD("
+WriteVal(r1,1.0)+","+WriteVal(r2,1.0)+") = "
+WriteVal(MACD_mod,1.2)+","+EncodeColor(Signalcolor)+" Signal("+ WriteVal(r1,1.0)+","
+WriteVal(r2,1.0)+","+WriteVal(r3,1.0)+") = "+WriteVal(MACDSignal_mod,1.2)+" ";