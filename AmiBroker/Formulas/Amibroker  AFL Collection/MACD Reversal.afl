/*							MACD Reversal.afl - Bill Barnard, 11/27/05
	This is to be used as an include file for SIGNAL STUDY.afl or the entire contents are to be
	pasted into SIGNAL OPTIMIZE.afl. Read discussion (first parameter) in SIGNAL STUDY.afl 	*/

//SetOption("EveryBarNullCheck", True);

AA		=  (Status("action")==3 OR Status("action")==4 OR Status("action")==5);	//	= 1 in AA
if (AA)	sigSelect = "MACD Reversal";
if (sigSelect=="MACD Reversal")	{

if (AA)	//	if this code is being used in AA
{
  	//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	Current		= ParamToggle("Use Current Symbol?", "Using Symbol named just below for Indicator,Using Current Symbol for Indicator");
	indicSym		= ParamStr("Indicator Symbol", "RUT-I");
	param1		= Optimize("long",  14, 1, 15, 1);
	param2		= Optimize("short", 10, 1, 14, 1);
	param2		= Min(Param1 - 1, Param2);
	param3		= 4;//Optimize("Rev%", 	7, 8, 18, 1);
	param3		= 0.001*param3;
	param6		= 1;//Optimize("AveTyp",  1, 0, 5, 1);
	param7		= ParamToggle("Standard number series or Fibonacci?",
								"Using standard number series|Using Fibonacci series");
	//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
}

//	if this code is being used for an indicator, retrieve stored param information
if (Status("action")==1)
{
	MRParamsList			= "";
	MRParamsFileName	= "ConfigFiles\\MRParams"+GetChartID()+".bf";

	fh 						= fopen( MRParamsFileName, "r");
	if( fh )
	{
		while( ! feof( fh ) )
		{
			MRParamsList = fgets( fh );
		}
		fclose( fh );
	}

	q0		=		IIf(MRParamsList !="", 	StrToNum(StrExtract(MRParamsList,  0	)), 0	 		);//	Current
	q1		= WriteIf(MRParamsList !="", 				  StrExtract(MRParamsList,   1	), 	"RUT-I"	);//	indicSym
	q2		=		IIf(MRParamsList !="", 	StrToNum(StrExtract(MRParamsList,  2	)), 90	 		);//	param1
	q3		=		IIf(MRParamsList !="", 	StrToNum(StrExtract(MRParamsList,  3	)), 45			);//	param2
	q4		=		IIf(MRParamsList !="", 	StrToNum(StrExtract(MRParamsList,  4	)), 10	 		);//	param3
	q5		=		IIf(MRParamsList !="", 	StrToNum(StrExtract(MRParamsList,  5	)), 5	 		);//	param4
	q6		=		IIf(MRParamsList !="", 	StrToNum(StrExtract(MRParamsList,  6	)), 95			);//	param5
	q7		=		IIf(MRParamsList !="", 	StrToNum(StrExtract(MRParamsList,  7	)), 1	 		);//	param6
	q8		=		IIf(MRParamsList !="", 	StrToNum(StrExtract(MRParamsList,  8	)), 0			);//	param7
	q9		=		IIf(MRParamsList !="", 	StrToNum(StrExtract(MRParamsList,  9	)), 0	 		);//	param8


	_SECTION_BEGIN("INDICATOR PARAMETERS");
	Current		= ParamToggle("MACD Reversal -                  Use Current Symbol?", 
						"Symbol below makes Indicator - click to change,Current Symbol makes Indicator - click to change", q0);
	IndicString	= ParamStr("MACD Reversal -                         Indicator Symbol", "Indicator Symbol---"+q1);
	indicSym		= StrMid(StrToUpper(IndicString), 19, 5) ;
	param1		= Param("MACD Reversal -                  Long Average Period",  q2, 1, 250, 1);
	param2		= Param("MACD Reversal -                  Short Average Period", q3, 1, 199, 1);
	param2		= Min(Param1 - 1, Param2);
	param3		= Param("MACD Reversal -                         Reversal Amount",  q4, 0, 20, 0.1);
	param3		= 0.001*param3;
	param4		= q5;//Param("MACD Reversal                                 Buy Minimum",  q5, 0, 100, 1);
	param5		= q6;//Param("MACD Reversal                                Sell Maximum",  q6, 0, 100, 1);
	param6	= ParamList("MACD Reversal -                             Average Type", "Simple Moving Average,"+
								"Exponential Moving Average,Double Moving Average,Triple Moving Average,"+
								"Weighted Moving Average,Till3 Moving Average", q7);
	param6	= 	IIf(param6=="Simple Moving Average", 0, IIf(param6=="Exponential Moving Average", 1,
					IIf(param6=="Double Moving Average", 2, IIf(param6=="Triple Moving Average", 3,
					IIf(param6=="Weighted Moving Average", 4, 5)))));
	param7		= ParamToggle("MACD R -   Standard number series or Fibonacci?",
								"Using standard number series - click to change|Using Fibonacci series - click to change", q8);
	param8		= q9;
	_SECTION_END();
}

//	Buy/Sell rules start here
aveStr		= "MA,EMA,DEMA,TEMA,WMA,Till3";
aveType 		= StrExtract(aveStr, Param6);

tmp1		= Param1;	tmp2		= Param2;
if (Param7)
{
	Param1	= Min(Param1, 15);
	Param1	= Max(Param1, 3);
	Param2	= Min(Param1 - 1, Param2);
	Param1 	= fib(Param1);
	Param2	= fib(Param2);
}

ind1		= IIf(current, Close, Foreign(indicSym, "Close"));
Nam		= WriteIf(current, Name(), indicSym);		Name1	= Nam+"  "+aveType+"  MACD "+V0(param1)+", "+V0(param2);
indError	= IIf(IsNull(ind1), 1, 0);
indicSym	= WriteIf(indError, "", indicSym);
indstart	= param1 + param2;
slow		= MovAve(aveType, ind1, param1);
fast		= MovAve(aveType, ind1, param2);
diff		= 100000 + 1000*(fast - slow) / slow;
param1	= tmp1;
param2	= tmp2;

z			= Zig(diff, param3);

bot 		= ROC(z, 1) < 0 AND Ref(ROC(z, 1), 1) > 0;
top		= ROC(z, 1) > 0 AND Ref(ROC(z, 1), 1) < 0;
recValley	= ValueWhen(bot, diff, 1);
BuyLine	= IIf(BarsSince(top) > BarsSince(bot) OR BarsSince(top)==0
				OR IsEmpty(BarsSince(top)),  (1 + 0.01*param3)*recValley, Null);
recPeak	= ValueWhen(top, diff, 1);
SellLine	= IIf(BarsSince(top) < BarsSince(bot) OR BarsSince(bot)==0
				OR IsEmpty(BarsSince(bot)),  (1 - 0.01*param3)*recPeak, Null);

xplot1	= ind1;
xplot2	= slow;
xplot3	= fast;
ind1		= Null;	//we are  through with it and don't want it to be plotted
ind2		= diff - 100000;														Name2	= "10*Diff %"+w2(ind2);
ind3		= IIf(NOT IsEmpty(BuyLine), BuyLine - 100000, BuyLine);	Name3	= "BuyLine"+w2(ind3);
ind4		= IIf(NOT IsEmpty(SellLine), SellLine - 100000, SellLine);	Name4	= "SellLine"+w2(ind4);
//ind3		= BuyLine - 100000;													Name3	= "BuyLine"+w2(ind3);
//ind4		= SellLine - 100000;													Name4	= "SellLine"+w2(ind4);
ind5		= 1000*param3;														Name5	= "\nReversal"+w1(ind5);
param3	= ind5;
ind5		= Null;	//we are  through with it and don't want it to be plotted


BuyCond	= IIf(BarIndex() > indstart, ind2 >= ind3, 0);
SellCond	= IIf(BarIndex() > indstart,ind4 >= ind2, 0);

Buy		= ExRem(BuyCond, SellCond);
Sell		= ExRem(SellCond, BuyCond);

if (Status("action")==1)	// if this code is being used for an indicator
{
	//	make an initiation file to store params
	MRParamsList = ""+
		Current		+","+
		indicSym		+","+
		param1		+","+
		param2		+","+
		param3		+","+
		param4		+","+
		param5		+","+
		param6		+","+
		param7		+","+
		param8		+","+
		"";

	fh 	= fopen( MRParamsFileName, "w");
	if( fh )
	{
		fputs(MRParamsList, fh);
		fclose( fh );
	}
}

showExtraPlots		= 1;

}