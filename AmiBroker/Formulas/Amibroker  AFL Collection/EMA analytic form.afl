The simple moving average is a Sum of the n last values divided by n.
for example, if n = 5 then
MA ( C , 5 ) = ( C + Ref ( C , - 1 ) + Ref ( C , - 2 ) + Ref ( C , -3 ) + Ref ( C , - 4 ) ) / 5
All five summands have the same coefficient, equal to 1.
A Weighted moving average has NOT equal coefficients, the most recent summands are the most important.
WMA ( C , 5 ) = ( 5 * C + 4 * Ref ( C , - 1 ) + 3 * Ref ( C , - 2 ) + 2 * Ref ( C , -3 ) + 1 * Ref ( C , - 4 ) ) / 15
The coefficients of the recent summands in the Exponential moving average are calculated by the following AFL code.

// The EMA analytic form, by D. Tsokakis, March 2005
Plot ( C , " " , colorBlack , 64 ) ;
p = 20 ;
x = EMA ( C , p ) ;
Plot ( x , " ", colorBlack , 1 ) ;
// 1st analysis
x = ( 2 * C + (p-1) * Ref ( x , -1 ) ) / ( p + 1 ) ;
// 2nd
x = ( 2 * ( p + 1 ) * C + 2 * ( p - 1 ) * Ref ( C , -1 ) + ( p - 1 ) ^2 * Ref ( x , -2 ) ) / ( p + 1 ) ^2 ;
// 3rd
x = ( 2 * ( p + 1 ) ^2 * C + 2 * ( p - 1 ) * ( p + 1 ) * Ref ( C , -1 ) + 2 * ( p - 1 ) ^2 * Ref ( C , -2 ) + ( p - 1 ) ^3 * Ref ( x , -3 ) ) / ( p + 1 ) ^3 ;
// 4th
x = ( 2 * ( p + 1 ) ^3 * C + 2 * ( p - 1 ) * ( p + 1 ) ^2 * Ref ( C , -1 ) + 2 * ( p - 1 ) ^2 * ( p + 1 ) * Ref ( C , -2 ) + 2 * ( p - 1 ) ^3 * Ref ( C , - 3 ) + ( p - 1 ) ^4 * Ref ( x , -4 ) ) / ( p + 1 ) ^4;
// The general N0-th analysis
N0 = 10 ; // calibrate the number of the previous terms
y = 0 ;
Title = "EMA ( C , " + WriteVal ( P , 1.0 ) + " ) =" ;
for ( n = 0 ; n < N0 ; n++ )
{
y = y + ( 2 * ( p - 1 ) ^n / ( p + 1 ) ^ ( n + 1 ) ) * Ref ( C , -n ) ;
Title = Title + "\n" + WriteVal ( 2 * (p - 1 ) ^n / ( p + 1 ) ^ ( n + 1 ) , 1.3 ) + WriteIf ( N == 0 , "* C +","* Ref ( C , -" + WriteVal ( n , 1.0 ) + ") +" ) ;
}
y = y + ( ( p - 1 ) / ( p + 1 ) ) ^n * Ref ( x , -n ) ;
Title = Title + "\n" + WriteVal ( ( ( p - 1 ) / ( p + 1 ) ) ^n , 1.3 ) + "* Ref ( EMA ( C , " + WriteVal ( p , 1.0 ) + " ) , -" + WriteVal( n , 1.0 ) + ")" ;
Plot ( y , "y" , colorRed , 8 ) ;

In the first lines we give some examples for the 1st, 2nd, 3rd AND 4th analysis.
Then the general N0-th analysis follows.
An EMA ( C, p ) is a Sum of summands
 ( 2 * ( p - 1 ) ^n / ( p + 1 ) ^ ( n + 1 ) ) * Ref ( C , -n )
plus a last term equal to
( ( p - 1 ) / ( p + 1 ) ) ^n * Ref ( x , -n )

This is the main difference of EMA, its value depends on the last Close values but also on the EMA value n bars ago !! 
As you may see for N0 = 10 OR N0 = 20, the coefficient of this "old" value is quite significant.
