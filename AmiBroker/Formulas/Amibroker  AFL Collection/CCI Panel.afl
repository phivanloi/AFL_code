/////////////////////////////// 

// CCI Panel for Amibroker

// Codded/Added by Dennis, Kris, Wring

// Last Update: 10/28/2007

/////////////////////////////// 

// Go to www.woodiescciclub.com to learn everything about this system.

// Go to www.woodiescciclub.com/forum/viewforum.php?f=48 to learn how tu use this panel.

// You must be a registered user to see the images and downloads.

/////////////////////////////// 

//Setup Axes and Grid section (right click on chart panel, click on Parameters):

// Scaling: Custom , Min=-275  Max=275

// Show Date Axis = Yes , Show Middle Lines = No

/////////////////////////////// 

// To activate the timer properly, make sure the following is set:

// click on Tools==>Preferences==>Intraday....

// make sure "Allign minute bars to market hours" is checked...

// make sure "Start time of interval" is checked...

// make sure "Override: Weekly/monthly bars use day of last trade" is checked.

/////////////////////////////// 

// Discalimer: For educational purposes only. Trade at your own risk.

/////////////////////////////// 

// Tick Multiple

TicMult= Param("Tick Multiple (ER2=10, YM=1, NQ=4, Stocks=1)",1,0,100);

// Background colors

SetChartBkColor(colorPaleBlue); // color of outer border 

// CCI lines colors

zperiod=Param("WCCI Period",14,0,100);
z = CCI(zperiod); 
z6period=Param("Turbo CCI Period",6,0,1000);
z6 = CCI(z6period); 

LSMA25 = LinearReg(C, 25 ); 
EMA34 = EMA(C,34); 
PI = atan(1.00) * 4; 
periods = 30; 
HighHigh = HHV(H, periods); 
LowLow = LLV(L, periods); 
range = 25 / (HighHigh - LowLow) * LowLow;

// Timer

TTMperiod = 6; 
Low_ma = EMA(L, TTMperiod); 
High_ma = EMA(H, TTMperiod); 
Low_third = (High_ma - Low_ma) / 3 + Low_ma; 
High_third = 2 * (High_ma - Low_ma) / 3 + Low_ma; 
tempnum = Now( 4 ) - TimeNum(); 
TimeRem = Interval() - ((int(tempnum[BarCount - 1] / 100) * 60) + (tempnum[BarCount - 1] - int(tempnum[BarCount - 1] / 100) * 100)); 
if (TimeRem[BarCount - 1] < 0) TimeRem = 0; 
TitleTimeRem = EncodeColor(colorWhite); 
MinuteVar = int(TimeRem );  
if (TimeRem[BarCount - 1] > 60) 
{ 
TitleTimeRem = TitleTimeRem + EncodeColor(colorWhite) + MinuteVar + "s"; 
} 
else if (TimeRem[BarCount - 1] > 45) 
{ 
TitleTimeRem = TitleTimeRem  +  EncodeColor(colorWhite) + MinuteVar + "s"; 
} 

else 
{ 
TitleTimeRem = TitleTimeRem +  EncodeColor(colorYellow) + MinuteVar + "s"; 
} 

// EMA34 Angle

x1_EMA34 = 0; 
x2_EMA34 = 1; 
y1_EMA34 = 0; 
y2_EMA34 = (Ref(EMA34, -1) - EMA34) / Avg * range; 
c_EMA34 = sqrt((x2_EMA34 - x1_EMA34)*(x2_EMA34 - x1_EMA34) + (y2_EMA34 - y1_EMA34)*(y2_EMA34 - y1_EMA34)); 
angle_EMA34 = round(180 * acos((x2_EMA34 - x1_EMA34)/c_EMA34) / PI); 
angle_EMA34 = IIf(y2_EMA34 > 0, - angle_EMA34, angle_EMA34); 

// LSMA25 Angle

x1_LSMA25 = 0; 
x2_LSMA25 = 1; 
y1_LSMA25 = 0; 
y2_LSMA25 = (Ref(LSMA25, -1) - LSMA25) / Avg * range; 
c_LSMA25 = sqrt((x2_LSMA25 - x1_LSMA25)*(x2_LSMA25 - x1_LSMA25) + (y2_LSMA25 - y1_LSMA25)*(y2_LSMA25 - y1_LSMA25)); 
angle_LSMA25 = round(180 * acos((x2_LSMA25 - x1_LSMA25)/c_LSMA25) / PI); 
angle_LSMA25 = IIf(y2_LSMA25 > 0, - angle_LSMA25, angle_LSMA25); 

// Color the bars for Woodies Trend Following 

Plusbars = BarsSince(z < 0); 
Minusbars = BarsSince(z > 0); 
TrendBarCount = 6; 
Color[0] = colorDefault; 
Trend[0] = 0; 
for( i = 1; i < BarCount; i++ ) 
{ 
if (Plusbars[i] >= TrendBarCount) 
{ 
Trend[i] = 1; 
} 
else if (Minusbars[i] >= TrendBarCount) 
{ 
Trend[i] = -1; 
} 
else 
{ 
Trend[i] = Trend[i - 1]; 
} 

if (Trend[i] == 1) 
{ 
if (Minusbars[i] == TrendBarCount - 1) 
{ 
Color[i] = colorYellow; 
} 
else if (z[i] < 0) 
{ 
Color[i] = colorDarkGrey; 
} 
else 
{ 
Color[i] = colorCustom7; 
} 
} 
else if (Trend[i] == -1) 
{ 
if (Plusbars[i] == TrendBarCount - 1) 
{ 
Color[i] = colorYellow; 
} 
else if (z[i] >= 0) 
{ 
Color[i] = colorDarkGrey; 
} 
else 
{ 
Color[i] = colorDarkRed; 
} 

} 
else 
{ 
Color[i] = colorDefault; 
} 
} 

// CCI Line 

Plot(z,"WCCI", colorBlack, styleLine | styleThick); 

// Turbo CCI 

Plot(z6,"TCCI", colorRed, styleLine | styleThick); 

// CCI Histogram 

Plot(z,"",Color,styleHistogram | styleThick | styleNoLabel); 

// Plot Grids

PlotGrid(0); 
PlotGrid(-100); 
PlotGrid(100);
PlotGrid(-200); 
PlotGrid(200); 

// Plot the Mock CZI on the 100s 

Cziparam= 5;
ColorANGLE_EMA = IIf(angle_EMA34 >=Cziparam,colorTurquoise,IIf(angle_EMA34 <=-1*Cziparam,colorDarkRed,colorYellow)); 
Plot(100,"", ColorANGLE_EMA , styleLine | styleThick | styleNoLabel); 
Plot(-100,"", ColorANGLE_EMA , styleLine | styleThick | styleNoLabel); 

// Plot the Mock Sidewinder on the 200s
 
TitleTrending = WriteIf((abs(angle_EMA34) >= 15) AND (abs(angle_EMA34 + angle_LSMA25) >= 50), EncodeColor (colorBrightGreen) + "TRENDING", 
WriteIf((abs(angle_EMA34) >= 5) AND (((angle_EMA34 >= 0) AND (angle_LSMA25 >= 0)) OR ((angle_EMA34 <= 0) AND (angle_LSMA25 <= 0))) AND (abs(angle_EMA34 + angle_LSMA25) >= 30), EncodeColor (colorYellow) + "NORMAL", 
EncodeColor (colorRed) + "FLAT")); 
SW = IIf((abs(angle_EMA34) >= 15) AND (abs(angle_EMA34 + angle_LSMA25) >= 50), IIf(angle_LSMA25 > 0, 2, -2), 
IIf((abs(angle_EMA34) >= 5) AND (((angle_EMA34 >= 0) AND (angle_LSMA25 >= 0)) OR ((angle_EMA34 <= 0) AND (angle_LSMA25 <= 0))) AND (abs(angle_EMA34 + angle_LSMA25) >= 30), IIf(angle_LSMA25 > 0, 1, -1), 0)); 
ColorSW = IIf(abs(SW) == 2, colorBrightGreen, 
IIf(abs(SW) == 1, colorYellow, colorRed)); 
Plot(200,"", ColorSW, styleLine | styleThick | styleNoLabel); 
Plot(-200,"", ColorSW, styleLine | styleThick | styleNoLabel);

// zero line 25lsma 

PlotShapes(IIf(C > LSMA25,shapeSmallCircle,shapeNone),colorBrightGreen,0,0,0); 
PlotShapes(IIf(C < LSMA25,shapeSmallCircle,shapeNone),colorRed,0,0,0);
PlotShapes(IIf(C == LSMA25,shapeSmallCircle,shapeNone),colorTeal,0,0,0);

// CCI Points

CCipointmove= round(abs(z-Ref(z,-1)));
CCIpointmovetitle= WriteIf(CCipointmove<15,EncodeColor(colorDarkRed) + "\n"  + "WCCI Points   " + " " + 
CCipointmove,WriteIf(CCipointmove>=15 AND CCipointmove<20,EncodeColor(colorYellow) + "\n"  + "WCCI Points   " + " " + 
CCipointmove,WriteIf(CCIpointmove>=20,EncodeColor(colorBrightGreen) + "\n"  + "WCCI Points   " + " " + 
CCipointmove,"")));

// Price Panel

Lastpricetitlehi= WriteIf(H>Ref(H,-1),EncodeColor(colorBrightGreen) + Ref(H,-1) + "  " + H , EncodeColor(colorBlack)+ Ref(H,-1) + "  " + H);
Lastpricetitlelo= WriteIf(L<Ref(L,-1),EncodeColor(colorDarkRed) + Ref(L,-1) + "  " + L , EncodeColor(colorBlack) + Ref(L,-1) + "  " + L);

//Pivot Points Calculations

DayH = TimeFrameGetPrice("H", inDaily, -1);// yesterdays high
DayL = TimeFrameGetPrice("L", inDaily, -1);//low 
DayC = TimeFrameGetPrice("C", inDaily, -1);//close 
DayO = TimeFrameGetPrice("O", inDaily);// current day open 
HiDay = TimeFrameGetPrice("H", inDaily); 
LoDay = TimeFrameGetPrice("L", inDaily); 
PP = (DayH + DayL + DayO + DayO) / 4 ; 
R1 = (2 * PP) - DayL; 
S1 = (2 * PP) - DayH; 
R2 = PP + R1 - S1; 
S2 = PP + S1 - R1; 
R3 = R2 + (R1 - PP); 
S3 = S2 - (PP - S1); 
ppvalue= abs(((round((C-pp)*100)*.01)*ticMult));
r1value= abs(((round((C-r1)*100)*.01)*ticMult));
s1value= abs(((round((C-s1)*100)*.01)*ticMult));
r2value= abs(((round((C-r2)*100)*.01)*ticMult));
s2value= abs(((round((C-s2)*100)*.01)*ticMult));
r3value= abs(((round((C-r3)*100)*.01)*ticMult));
s3value= abs(((round((C-s3)*100)*.01)*ticMult));
wptitle=  WriteIf(C>=R3, "\n" + EncodeColor(colorYellow)+"R3 " + "-" + round(R3value),
WriteIf(C<=R3 AND C>=R2, "\n" + EncodeColor(colorYellow)+"R3  " + "+" + round(R3value) + "\n" + EncodeColor(colorYellow) + "R2  " + "-" + round(R2value) + "\n", 
WriteIf(C<=R2 AND C>=R1, "\n" + EncodeColor(colorYellow)+"R2  " + "+" + round(R2value) + "\n" +  EncodeColor(colorYellow) + "R1  " + "-" + round(R1value)+ "\n",  
WriteIf(C<=R1 AND C>=PP, "\n" + EncodeColor(colorYellow)+"R1  " + "+" + round(R1value) + "\n" +  EncodeColor(colorYellow) + "PP  " + "-" + round(PPvalue) + "\n",  
WriteIf(C<=PP AND C>=S1, "\n" + EncodeColor(colorYellow)+"PP  " + "+" + round(PPvalue) + "\n" +  EncodeColor(colorYellow) + "S1  " + "-" + round(S1value) + "\n",  
WriteIf(C<=S1 AND C>=S2, "\n" + EncodeColor(colorYellow)+"S1  " + "+" + round(S1value) + "\n" +  EncodeColor(colorYellow) + "S2  " + "-" + round(S2value) + "\n",  
WriteIf(C<=S2 AND C>=S3, "\n" + EncodeColor(colorYellow)+"S2  " + "+" + round(S2value) + "\n" +  EncodeColor(colorYellow) + "S3  " + "-" + round(S3value) + "\n", 
WriteIf(C<=S3, "\n" + EncodeColor(colorYellow)+"S3  " + "-" + round(S3value) + "\n",""))))))));

// HOD , LOD, Range

HiDayTitle = EncodeColor(colorYellow) + "HOD  "; 
HiDayTitle = HiDayTitle + EncodeColor(colorYellow) + HiDay + "   +" + abs(((round((HiDay-C)*100)*.01)*ticMult));
LoDayTitle = EncodeColor(colorYellow) + "LOD  "; 
LoDayTitle = LoDayTitle + EncodeColor(colorYellow) + LoDay + "    -" + abs(((round((C-LoDay)*100)*.01)*ticMult));
RangeTitle1 = EncodeColor(colorYellow) + "Day Range    " + EncodeColor(colorYellow) + StrToNum(NumToStr(round((HiDay - LoDay)*TicMult), 4.4));

// Title

Title = "\n" + "" + Name() + ", " + Interval(2) + ", " + Date() + "\n" +
TitleTimeRem + EncodeColor(colorWhite) + "\n" + Titletrending  + "\n" +
Lastpricetitlehi + "\n" + EncodeColor(colorBlack) +  " " + Close +   
"\n" + Lastpricetitlelo + "\n" + wptitle + "\n" + HiDayTitle +  "\n" + LoDayTitle + "\n" + RangeTitle1 + "\n" + CCIpointmovetitle;
