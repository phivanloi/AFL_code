/*							Filtered Moving Average.afl - Bill Barnard, 11/29/05
	This is to be used as an include file for SIGNAL STUDY.afl or the entire contents are to be
	pasted into SIGNAL OPTIMIZE.afl. Read discussion (first parameter) in SIGNAL STUDY.afl 	*/

AA		=  (Status("action")==3 OR Status("action")==4 OR Status("action")==5);	//	= 1 in AA
if (AA)	sigSelect = "Filtered Moving Average";
if (sigSelect=="Filtered Moving Average")	{

if (AA)	//	if this code is being used in AA
{
	//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX    Optimize    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	Current		= ParamToggle("Use Current Symbol?", "Using Symbol named just below for Indicator,Using Current Symbol for Indicator - click to change");
	indicSym		= ParamStr("Indicator Symbol", "RUT-I");
	param1		= Optimize("Period",  36, 1, 101, 5);
	param2		= Optimize("Perc", 	2, 0, 15, 1);
	param6		= 1;//Optimize("AveTyp",  1, 0, 5, 1);
	//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
}

//	if this code is being used in an indicator, retrieve stored param information
if (Status("action")==1)
{
	FMAParamsList			= "";
	FMAParamsFileName	= "ConfigFiles\\FMAParams"+GetChartID()+".bf";

	fh 						= fopen( FMAParamsFileName, "r");
	if( fh )
	{
		while( ! feof( fh ) )
		{
			FMAParamsList = fgets( fh );
		}
		fclose( fh );
	}

	q0		=		IIf(FMAParamsList !="", 	StrToNum(StrExtract(FMAParamsList,  0	)), 0	 		);//	Current
	q1		= WriteIf(FMAParamsList !="", 				  StrExtract(FMAParamsList,   1	), 	"RUT-I"	);//	indicSym
	q2		=		IIf(FMAParamsList !="", 	StrToNum(StrExtract(FMAParamsList,  2	)), 50	 		);//	param1
	q3		=		IIf(FMAParamsList !="", 	StrToNum(StrExtract(FMAParamsList,  3	)), 1			);//	param2
	q4		=		IIf(FMAParamsList !="", 	StrToNum(StrExtract(FMAParamsList,  4	)), 0	 		);//	param3
	q5		=		IIf(FMAParamsList !="", 	StrToNum(StrExtract(FMAParamsList,  5	)), 0	 		);//	param4
	q6		=		IIf(FMAParamsList !="", 	StrToNum(StrExtract(FMAParamsList,  6	)), 0			);//	param5
	q7		=		IIf(FMAParamsList !="", 	StrToNum(StrExtract(FMAParamsList,  7	)), 1	 		);//	param6
	q8		=		IIf(FMAParamsList !="", 	StrToNum(StrExtract(FMAParamsList,  8	)), 0			);//	param7
	q9		=		IIf(FMAParamsList !="", 	StrToNum(StrExtract(FMAParamsList,  9	)), 0	 		);//	param8


	_SECTION_BEGIN("INDICATOR PARAMETERS");
	Current		= ParamToggle("Filtered Moving Average -      Use Current Symbol?", 
						"Symbol below makes Indicator - click to change,Current Symbol makes Indicator - click to change", q0);
	IndicString	= ParamStr("Filtered Moving Average -            Indicator Symbol", "Indicator Symbol---"+q1);
	indicSym		= StrMid(StrToUpper(IndicString), 19, 5) ;
	param1		= Param("Filtered Moving Average -              Average Period",  q2, 1, 250, 1);
	param2		= Param("Filtered Moving Average -  Buy/Sell Offset Percent", q3, 0, 10, 0.1);
	param3		= q4;
	param4		= q5;
	param5		= q6;
	param6	= ParamList("Filtered Moving Average -                Average Type", "Simple Moving Average,"+
								"Exponential Moving Average,Double Moving Average,Triple Moving Average,"+
								"Weighted Moving Average,Till3 Moving Average", q7);
	param6	= 	IIf(param6=="Simple Moving Average", 0, IIf(param6=="Exponential Moving Average", 1,
					IIf(param6=="Double Moving Average", 2, IIf(param6=="Triple Moving Average", 3,
					IIf(param6=="Weighted Moving Average", 4, 5)))));
	param7		= q8;
	param8		= q9;
	_SECTION_END();
}

//	Buy/Sell rules start here
aveStr		= "MA,EMA,DEMA,TEMA,WMA,Till3";
aveType 		= StrExtract(aveStr, Param6);

ind2		= IIf(current, Close, Foreign(indicSym, "Close"));
Nam		= WriteIf(current, Name(), indicSym);					Name2	= Nam + " Close"+w2(ind2);
indstart	= IIf(aveType=="Till3", 6*Param1, Param1);
ind5		= MovAve(aveType, ind2, Param1);
ind3		= ind5 + 0.01*param2*ind5;								Name3	= aveType+"("+Param1+")"+" + "+param2+"% "+w2(ind3);
ind4		= ind5 - 0.01*param2*ind5;									Name4	= aveType+"("+Param1+")"+" - "+param2+"% "+w2(ind4)+nl(1);
ind5		= Null;	// through with it and don't want to plot it

indError	= IIf(IsNull(ind2), 1, 0);
indicSym	= WriteIf(indError, "", indicSym);

Buy		= IIf(BarIndex() > indstart, Cross(ind2, ind3), 0);
Sell		= IIf(BarIndex() > indstart, Cross(ind4, ind2), 0);

BuyCond	= Flip(Buy, Sell);
SellCond	= Flip(Sell, Buy);

Buy		= ExRem(Buy, Sell);
Sell		= ExRem(Sell, Buy);

if (Status("action")==1)	// if this code is being used for an indicator
{
	//	make an initiation file to store params
	FMAParamsList = ""+
		Current		+","+
		indicSym		+","+
		param1		+","+
		param2		+","+
		param3		+","+
		param4		+","+
		param5		+","+
		param6		+","+
		param7		+","+
		param8		+","+
		"";

	fh 	= fopen( FMAParamsFileName, "w");
	if( fh )
	{
		fputs(FMAParamsList, fh);
		fclose( fh );
	}
}

}